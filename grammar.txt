statements  : BAGONGLINYA * statement (BAGONGLINYA + statement) * BAGONGLINYA *

statement	: SUSIPULONG: IBALIK expr?
			: SUSIPULONG: PADAYON
			: SUSIPULONG: PUTOL
			: expr

expr        : SUSIPULONG: BARYABOL TIGPAILA EQ expr
            : comp-expr ((SUSIPULONG: UG | SUSIPULONG: O) comp-expr)*

comp-expr   : DILI comp-expr
            : arith-expr ((EE|LT|GT|LTE|GTE) arith-expr)*

arith-expr  : term ((DUGANG | TANGGAL) term)*

term        : factor ((DAGHANON | BAHINON) factor)*

factor      : (DUGANG | TANGGAL) factor
            : power

power       : call (GAHOM factor)*

call        : atom (LPAREN (expr (COMMA expr)*)? RPAREN)?

atom        : NUMERO | LUTAW | SULAT | TIGPAILA
            : LPAREN expr RPAREN
            : list-expr
            : if-expr
            : for-expr
            : while-expr
            : func-def

list-expr   : LSQUARE (expr (COMMA expr)*)? RSQUARE

if-expr     : SUSIPULONG: KUNG expr SUSIPULONG: KAY
              (statement if-expr-b|if-expr-c?)
            | (BAGONGLINYA statements SUSIPULONG: HUMAN | if-expr-b | if-expr-c)

if-expr-b   : SUSIPULONG: PUWEDEPUD expr SUSIPULONG: KAY
              (statement if-expr-b | if-expr-c?)
            | (BAGONGLINYA statements SUSIPULONG: HUMAN | if-expr-b | if-expr-c)

if-expr-c   : SUSIPULONG: KUNGDILI
              statement
            | (BAGONGLINYA statements SUSIPULONG: HUMAN)

for-expr    : SUSIPULONG: PARA TIGPAILA EQ expr SUSIPULONG: SA expr
              (SUSIPULONG: LAKANG expr)? SUSIPULONG: KAY
              statement
            | (BAGONGLINYA statements SUSIPULONG: HUMAN)

while-expr  : SUSIPULONG: SAMTANG expr SUSIPULONG: KAY
              statement
            | (BAGONGLINYA statements SUSIPULONG: HUMAN)

func-def    : SUSIPULONG: KALIHOKAN TIGPAILA?
              LPAREN (TIGPAILA (KOMA TIGPAILA)*)? RPAREN
              (PANA expr)
            | (BAGONGLINYA statements TIGPAILA: HUMAN)

###########################################################
# Translation from BISAYA to ENGLISH

TOKENS:
    'NUMERO'       = 'INT'
    'LUTAW'        = 'FLOAT'
    'SULAT'        = 'STRING'
    'TIGPAILA'     = 'IDENTIFIER'
    'SUSIPULONG'   = 'KEYWORD'
    'DUGANG'       = 'ADD'
    'TANGGAL'      = 'SUBTRACT'
    'DAGHANON'     = 'MULTIPLY'
    'BAHINON'      = 'DIVIDE'
    'GAHOM'        = 'POWER'
    'KOMA'         = 'COMMA'
    'PANA'         = 'ARROW'
    'BAGONGLINYA'  = 'NEWLINE'

KEYWORDS:
    'BARYABOL'     = 'VARIABLE'
    'UG'           = 'AND'
    'O'            = 'OR'
    'DILI'         = 'NOT'
    'KUNG'         = 'IF'
    'PUWEDEPUD'    = 'ELIF'
    'KUNGDILI'     = 'ELSE'
    'PARA'         = 'FOR'
    'SA'           = 'TO'
    'LAKANG'       = 'STEP'
    'SAMTANG'      = 'WHILE'
    'KALIHOKAN'    = 'FUNCTION'
    'KAY'          = 'THEN'
    'HUMAN'        = 'END'
    'IBALIK'       = 'RETURN'
    'PADAYON'      = 'CONTINUE'
    'PUTOL'        = 'BREAK'
    'PATAY'        = 'KILL'
